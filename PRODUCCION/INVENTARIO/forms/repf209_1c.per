DATABASE aceros
SCREEN
{

 No Pre-Venta:[g0     ]                            Fecha:[g1                 ]
       Moneda:[c ] [g12                  ]        Estado:[f|g28          ]  
       Bodega:[a ] [g14                  ] 	     IVA:[g4   ]%
  Grupo Línea:[g2   ] [g3                     ]   Dscto.:[g9   ]%   
      Cliente:[g5    ][g6                                                    ]
    Dirección:[g8                             ]  Ced/RUC/Pas:[g7             ]
     Vendedor:[g10 ] [g11                     ]    Tipo Pago:[g|g40    ]

    P  C.S.    C.D.    Bd   Item        Des %   Precio Unit.     Subtotal
   [b][g16    |g17    |z1] [g18      ] [g20  |g21           |g22             ]
   [b][g16    |g17    |z1] [g18      ] [g20  |g21           |g22             ]
   [b][g16    |g17    |z1] [g18      ] [g20  |g21           |g22             ]
   [b][g16    |g17    |z1] [g18      ] [g20  |g21           |g22             ]
                  		                   Subtotal:[g23             ]
     [g19                           ]             Descuento:[g24             ]
	 Peso Total:[c00            ]              Impuesto:[g25             ]
              Flete:[g15            ]	               Neto:[g26             ]
}
TABLES
	rept023
	rept024
	rept001
	rept002
	rept010
	gent020
	gent000
	gent013
ATTRIBUTES

c00  = FORMONLY.vm_tot_peso TYPE DECIMAL(11,3), REVERSE, NOENTRY, 
	FORMAT = '###,###,##&.###';
g0  = rept023.r23_numprev, REVERSE, NOENTRY;
g1  = FORMONLY.vm_fecha TYPE DATETIME YEAR TO SECOND, REVERSE, NOENTRY;
g2  = rept023.r23_grupo_linea , REVERSE, UPSHIFT, 
	COMMENTS = 'Grupo de Línea a la que pertenecen los Items. ';
g3  = FORMONLY.nom_grupo, NOENTRY;
g4  = rept023.r23_porc_impto, REVERSE, NOENTRY;
g5  = rept023.r23_codcli, UPSHIFT, 
	COMMENTS = 'Obligatorio para ventas a crédito.';
g6  = rept023.r23_nomcli, REVERSE, UPSHIFT,
	COMMENTS = 'Nombre del cliente. ';
g8  = rept023.r23_dircli, REVERSE, UPSHIFT,
	COMMENTS = 'Dirección del domicilio del cliente.  ';
g7  = rept023.r23_cedruc, REVERSE, UPSHIFT,
	COMMENTS = 'Cédula o RUC del cliente. ';
g9  = rept023.r23_descuento, REVERSE, FORMAT = '#&.##', 
	INCLUDE = (0.00 TO 99.99);
g   = rept023.r23_cont_cred, REVERSE, UPSHIFT, AUTONEXT,
	INCLUDE = ('C','R'), COMMENTS = 'Tipos de Pago: C Contado R Crédito';
g40 = FORMONLY.tit_cont_cred,REVERSE,UPSHIFT,NOENTRY;
c   = rept023.r23_moneda, REVERSE, UPSHIFT, AUTONEXT,
	COMMENTS = 'Moneda Base o Moneda Alterna. ';
g12 = FORMONLY.nom_moneda TYPE VARCHAR, NOENTRY;
g10 = rept023.r23_vendedor, REVERSE;
g11 = FORMONLY.nom_vendedor TYPE VARCHAR, NOENTRY;
a   = r23_bodega, REVERSE, UPSHIFT,COMMENTS = 'Bodega que Factura. ';
g14 = FORMONLY.nom_bodega TYPE VARCHAR, NOENTRY;
g15 = rept023.r23_flete, REVERSE, FORMAT = '####,###,##&.##',
	COMMENTS = 'En caso de que haya un recargo por transportación. ',
	INCLUDE = (0 TO 999999999.99);
b   = rept024.r24_proformado, NOENTRY, AUTONEXT,
	INCLUDE = ('S','N'), DEFAULT = 'N';
g16 = rept024.r24_cant_ped,FORMAT = '###&.##',
	INCLUDE = (1 TO 9999);
g17 = rept024.r24_cant_ven, NOENTRY, FORMAT = '###&.##';
z1  = r24_bodega, UPSHIFT, AUTONEXT, REQUIRED;
g18 = rept024.r24_item, UPSHIFT, AUTONEXT;
g19 = FORMONLY.nom_item, NOENTRY;
g20 = rept024.r24_descuento, FORMAT = '#&.##', INCLUDE = (0 TO 99.99);
g21 = rept024.r24_precio, NOENTRY, FORMAT = '###,###,##&.##';
g22 = FORMONLY.subtotal_item TYPE DECIMAL(12,2), NOENTRY, 
	FORMAT = '#,###,###,##&.##';
g23 = rept023.r23_tot_bruto,  
	FORMAT = '#,###,###,##&.##',NOENTRY;
g24 = rept023.r23_tot_dscto, 
	FORMAT = '#,###,###,##&.##', NOENTRY;
g25 = FORMONLY.vm_impuesto TYPE DECIMAL(11,2), 
	FORMAT = '#,###,###,##&.##', NOENTRY;
g26 = rept023.r23_tot_neto , REVERSE, FORMAT = '#,###,###,##&.##', NOENTRY;
g28 = FORMONLY.tit_estado TYPE VARCHAR, NOENTRY;
f   = rept023.r23_estado, REVERSE, NOENTRY, UPSHIFT;
end
INSTRUCTIONS 
DELIMITERS '||'
SCREEN RECORD r_detalle[4] (rept024.r24_proformado, rept024.r24_cant_ped, 
			    rept024.r24_cant_ven,   rept024.r24_bodega,
			    rept024.r24_item, 
			    rept024.r24_descuento,  rept024.r24_precio, 
			    FORMONLY.subtotal_item)

--#KEYS
--#	F5 = 'Crear Cliente'
--#	F7 = 'Otros Datos'
--#	F8 = '% Defaults'
