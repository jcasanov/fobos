DATABASE aceros
SCREEN size 18 by 78 TITLE '   Datos Item   '
{
                                                            [x000 ] de [y000 ]
  Código Item: [f001   ]                              Estado: [a][aa         ]
       Nombre: [f002                                                         ]

     Division: [z]        [a5                                  ]
        Línea: [f5]       [f054                                ]
        Grupo: [f55]      [f056                                    ]
        Clase: [f057    ] [F058                                              ]
        Marca: [f59   ]   [f060                                ]

         Tipo: [f003] [a2                                   ]
         Peso: [f004     ] 

Unidad Medida: [f005   ] [a3                                ]
 Cant. X Paq.: [f006     ]                      Sustituye a: [c051           ]
                                             Sustituido por: [c052           ]
       Modelo: [f009                ]             Secuencia: [c090      ]
      Partida: [f008           ] [a4                                         ]
}
SCREEN size 18 by 78 TITLE ' Valoración Item '
{
               Codigo de Item: [c100   ]                    [x001 ] de [y001 ]
                   Precio FOB: [f011         ]
               Moneda del FOB: [a1] [a7                            ]
           Filtro de Busqueda: [c050      ]

  	   P.V.P. Moneda Base: [f012          ]      Paga Impuesto?: [c]
        P.V.P. Moneda Alterna: [f013          ]
            Costo Moneda Base: [f014          ]    
         Costo Moneda Alterna: [f015          ]
     Costo Ultimo Moneda Base: [f016          ]
  Costo Ultimo Moneda Alterna: [f017          ]

          Costo de Reposición: [f072          ]
         Usuario que Modificó: [f073      ] Usuario Cambio Precio:[f080      ]
        Fecha de Modificación: [f074               ]
	  Fecha Cambio Precio: [f024               ]
          Cantidad en Pedidos: [f018         ] Cant.Backorders:[f019         ]
              Precio Anterior: [f023          ]
}
SCREEN size 18 by 78 TITLE '  Otros  Datos  '
{
               Codigo de Item: [c101   ]                    [x002 ] de [y002 ]
           Indice de Rotación: [a0] [a6                            ]

                 Stock Maximo: [f070    ]          Stock Mínimo: [f071    ]

	   	    Proveedor: [f061    ] [f062                           ]
	     Código Electrico: [f063         ]
                               [f064                               ]
	        Código Pedido: [f065                ] 
	     Código Comercial: [f067                               ] 
	                       [f067                               ] 
	      Código Utilidad: [f078 ] 

         Volumen Pies Cúbicos: [f075  ]
        Días de Mantenimiento: [f076  ]
           Días de Inventario: [f077  ]

               Actualizar Precios desde otra Localidad?: [f]
}
SCREEN size 18 by 78 TITLE '  Comentarios   '
{
               Codigo de Item: [c102   ]                    [x003 ] de [y003 ]

  Serie / Lote: [f079          ]



   Comentarios: [f020                                                        ]
                [f020                                                        ]


 Ingresado Por: [f025      ]           Fecha de Ingreso: [f026               ]
                                   Fecha de Eliminación: [f027               ]

}
TABLES
rept010
rept087
ATTRIBUTES
x000 = FORMONLY.vm_row_current1 TYPE SMALLINT, NOENTRY;
y000 = FORMONLY.vm_num_rows1 TYPE SMALLINT, NOENTRY;
x001 = FORMONLY.vm_row_current2 TYPE SMALLINT, NOENTRY;
y001 = FORMONLY.vm_num_rows2 TYPE SMALLINT, NOENTRY;
x002 = FORMONLY.vm_row_current3 TYPE SMALLINT, NOENTRY;
y002 = FORMONLY.vm_num_rows3 TYPE SMALLINT, NOENTRY;
x003 = FORMONLY.vm_row_current4 TYPE SMALLINT, NOENTRY;
y003 = FORMONLY.vm_num_rows4 TYPE SMALLINT, NOENTRY;
f001 = rept010.r10_codigo, REVERSE, UPSHIFT, NOENTRY, 
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f002 = rept010.r10_nombre, REVERSE, AUTONEXT, SCROLL;
a    = rept010.r10_estado, REVERSE, NOENTRY, UPSHIFT;
aa   = FORMONLY.tit_estado TYPE VARCHAR, NOENTRY;
f003 = rept010.r10_tipo, REVERSE, 
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
a2   = FORMONLY.nom_tipo TYPE VARCHAR, NOENTRY, REVERSE;
f004 = rept010.r10_peso, REVERSE, FORMAT = '#,##&.###';
f005 = rept010.r10_uni_med, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
a3   = FORMONLY.nom_uni TYPE VARCHAR, NOENTRY, REVERSE;
f006 = rept010.r10_cantpaq, REVERSE, FORMAT = '##,##&.##';
f    = rept010.r10_cantveh, REVERSE, FORMAT = '&', INCLUDE = (1, 0),
	COMMENTS = '(1) Para mantener el Precio Actual y (0) Para cambiar el Precio.';
f008 = rept010.r10_partida, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
a4   = FORMONLY.nom_par TYPE VARCHAR, NOENTRY, REVERSE;
f009 = rept010.r10_modelo, AUTONEXT, REVERSE, UPSHIFT;
z    = rept010.r10_linea ,REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
a5   = FORMONLY.nom_lin TYPE VARCHAR, NOENTRY, REVERSE;
a0 = rept010.r10_rotacion, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
a6   = FORMONLY.nom_rot TYPE VARCHAR, NOENTRY, REVERSE;
c = rept010.r10_paga_impto, REVERSE,
	WIDGET = 'CHECK', CONFIG = 'S N acknowedge';
f011 = rept010.r10_fob, REVERSE, FORMAT = '#,###,##&.###';
a1 = rept010.r10_monfob, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
a7   = FORMONLY.nom_mon TYPE VARCHAR, NOENTRY, REVERSE;
f012 = rept010.r10_precio_mb, REVERSE, FORMAT = '###,###,##&.##',
	INCLUDE = (0.00 TO 99999);
f013 = rept010.r10_precio_ma, NOENTRY, REVERSE, FORMAT = '###,###,##&.##';
f014 = rept010.r10_costo_mb, NOENTRY,  REVERSE, FORMAT  =  '###,###,##&.##';
f015 = rept010.r10_costo_ma, NOENTRY, REVERSE, FORMAT   = '###,###,##&.##';
f016 = rept010.r10_costult_mb, NOENTRY, REVERSE, FORMAT = '###,###,##&.##';
f017 = rept010.r10_costult_ma, NOENTRY, REVERSE, FORMAT = '###,###,##&.##';
f072 = rept010.r10_costrepo_mb, AUTONEXT, UPSHIFT, FORMAT = '---,---,--&.##';
f073 = rept010.r10_usu_cosrepo, NOENTRY, UPSHIFT;
f074 = rept010.r10_fec_cosrepo, NOENTRY, UPSHIFT;
f018 = rept010.r10_cantped, REVERSE, NOENTRY, FORMAT = '##,###,##&.##';
f019 = rept010.r10_cantback, REVERSE, NOENTRY, FORMAT = '--,---,--&.##';
f079 = rept010.r10_serie_lote, UPSHIFT, AUTONEXT, 
	WIDGET = 'RADIO', CONFIG = 'S Seriado  L Lote  G Genérico';
f020 = rept010.r10_comentarios, AUTONEXT, UPSHIFT, WORDWRAP;
f023 = rept010.r10_precio_ant, REVERSE, NOENTRY, FORMAT = '###,###,##&.##';
f024 = rept010.r10_fec_camprec, NOENTRY; 
f080 = rept087.r87_usu_camprec, NOENTRY, UPSHIFT;
f025 = rept010.r10_usuario, REVERSE, NOENTRY, UPSHIFT;
f026 = rept010.r10_fecing, REVERSE, NOENTRY, UPSHIFT;
f027 = rept010.r10_feceli, NOENTRY, UPSHIFT;

c050 = rept010.r10_filtro, UPSHIFT;
c051 = FORMONLY.vm_sustituye,  NOENTRY, REVERSE;
c052 = FORMONLY.vm_sustituido, NOENTRY, REVERSE;

f5   = rept010.r10_sub_linea, AUTONEXT, UPSHIFT, REVERSE,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f054 = FORMONLY.tit_sub_linea, NOENTRY, UPSHIFT, REVERSE;
f55  = rept010.r10_cod_grupo, AUTONEXT, UPSHIFT, REVERSE,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f056 = FORMONLY.tit_grupo, NOENTRY, UPSHIFT, REVERSE;
f057 = rept010.r10_cod_clase, AUTONEXT, UPSHIFT, REVERSE,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f058 = FORMONLY.tit_clase, NOENTRY, UPSHIFT, REVERSE;
f59  = rept010.r10_marca, AUTONEXT, UPSHIFT, REVERSE,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f060 = FORMONLY.tit_marca, NOENTRY, UPSHIFT, REVERSE;
f061 = rept010.r10_proveedor, UPSHIFT, 
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f062 = FORMONLY.nom_prov TYPE VARCHAR, NOENTRY;
f063 = rept010.r10_electrico, UPSHIFT, 
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f064 = FORMONLY.elec_desc TYPE VARCHAR, NOENTRY;
f065 = rept010.r10_cod_pedido,UPSHIFT,AUTONEXT;
f067 = rept010.r10_cod_comerc,UPSHIFT,AUTONEXT,WORDWRAP COMPRESS;
f078 = rept010.r10_cod_util, AUTONEXT, UPSHIFT, REVERSE,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
f075 = rept010.r10_vol_cuft, AUTONEXT, UPSHIFT, FORMAT = '###.##';
f076 = rept010.r10_dias_mant, AUTONEXT, UPSHIFT;
f077 = rept010.r10_dias_inv, AUTONEXT, UPSHIFT;
f070 = rept010.r10_stock_max, AUTONEXT, UPSHIFT, INCLUDE = (NULL,0 TO 100000);
f071 = rept010.r10_stock_min, AUTONEXT, UPSHIFT, INCLUDE = (NULL,0 TO 100000);
c090 = rept010.r10_sec_item, AUTONEXT, REVERSE, UPSHIFT, INCLUDE = (0 TO 10000),
	COMMENTS = 'Secuencia item definida por el usuario.';

c100 = FORMONLY.tit_item1, NOENTRY, REVERSE, UPSHIFT;
c101 = FORMONLY.tit_item2, NOENTRY, REVERSE, UPSHIFT;
c102 = FORMONLY.tit_item3, NOENTRY, REVERSE, UPSHIFT;
END
