DATABASE aceros 
SCREEN 
{

 Ingreso No: [c0|c001    ]            Fecha Proceso: [c007                ]
  Area Neg.: [c03] [c052            ]        Estado: [c099     ]
 Referencia: [c100                                                        ]
             [c100                                                        ]
    Cliente: [c004      ] [c005                                           ]
     Moneda: [a1] [c051           ]    Valor a Recaudar: [c006            ]
 --------------------------------------------------------------------------
  FP   Mon Bco/Tarj   Nro. Che./Aut.     Nro. Cta./tarj.        Valor
 --------------------------------------------------------------------------
 [a5] [a6] [c024  ] [c025           |c026                |c023            ]
 [a5] [a6] [c024  ] [c025           |c026                |c023            ]
 [a5] [a6] [c024  ] [c025           |c026                |c023            ]
 [a5] [a6] [c024  ] [c025           |c026                |c023            ]
 [a5] [a6] [c024  ] [c025           |c026                |c023            ]
 ---------------------------------------------------------------------------
                                   Total Moneda Factura: [c050            ]
 Ingresado Por: [c016      ]                     Vuelto: [c055            ]   
}
TABLES
cajt010
cajt011
ATTRIBUTES
c0   = cajt010.j10_tipo_fuente, NOENTRY, REVERSE;
c001 = cajt010.j10_num_fuente, NOENTRY, REVERSE;
c03  = cajt010.j10_areaneg, REVERSE,
	COMMENTS='Presione F2 para obtener ayuda del campo';
c004 = cajt010.j10_codcli, REVERSE,
	COMMENTS='Presione F2 para obtener ayuda del campo';
c005 = cajt010.j10_nomcli, REVERSE, UPSHIFT;
a1 = cajt010.j10_moneda, REVERSE, UPSHIFT, AUTONEXT,
	COMMENTS='Presione F2 para obtener ayuda del campo';
c006 = cajt010.j10_valor, REVERSE, FORMAT='#,###,###,##&.##',
	INCLUDE=(0 TO 9999999999.99);
c007 = cajt010.j10_fecha_pro, NOENTRY;

c016 = cajt010.j10_usuario, REVERSE, NOENTRY, UPSHIFT;

a5 = cajt011.j11_codigo_pago, UPSHIFT, AUTONEXT, 
	COMMENTS='Ingrese la forma de pago.';
a6 = cajt011.j11_moneda, UPSHIFT, AUTONEXT;
c023 = cajt011.j11_valor, FORMAT='#,###,###,##&.##'; 
c024 = cajt011.j11_cod_bco_tarj, UPSHIFT,
	COMMENTS='Ingrese el código del banco o de la tarjeta de crédito';
c025 = cajt011.j11_num_ch_aut, UPSHIFT,
	COMMENTS='Ingrese el número del cheque o de la autorización de la tarjeta';
c026 = cajt011.j11_num_cta_tarj, UPSHIFT, 
	COMMENTS='Ingrese el número de la cuenta o de la tarjeta';

c050 = FORMONLY.total_mf TYPE DECIMAL(12,2), 
	REVERSE, FORMAT='#,###,###,##&.##', NOENTRY; 
c055 = FORMONLY.vuelto TYPE DECIMAL(12,2), 
	REVERSE, FORMAT='#,###,###,##&.##', NOENTRY; 
c051 = FORMONLY.n_moneda, NOENTRY;
c052 = FORMONLY.n_areaneg, NOENTRY;

c100 = cajt010.j10_referencia, UPSHIFT, WORDWRAP COMPRESS;

c099 = FORMONLY.estado, REVERSE, NOENTRY;

end

INSTRUCTIONS
DELIMITERS '||'
SCREEN RECORD ra_j11[5] (j11_codigo_pago, j11_moneda, j11_cod_bco_tarj, j11_num_ch_aut, j11_num_cta_tarj, j11_valor)
