DATABASE diteca
SCREEN TITLE '  Datos Cliente  ' 
{
                                                              [x001] de [y001]

          Código Cliente: [c000       ]                 Estado: [c][c0a0     ]
          Nombre Cliente: [c001                                              ]
                          [c001                                              ]

              Personería               Documento Identidad: [c012           ]
             [c0b0         ]                                [c0c0           ]



     Dirección Domicilio: [c002                                    ]
      Teléfono Domicilio: [c004      ]

                 Usuario: [c014      ]
        Fecha de Ingreso: [c015               ]
}
SCREEN TITLE 'Datos Adicionales'
{
                                                              [x002] de [y002]

            Tipo Cliente: [c011  ] [c018                           ]

       Dirección Trabajo: [c003                                    ]
        Teléfono Trabajo: [c005      ]

                     Fax: [c006       ]
           Fax Adicional: [c007       ]
                 Casilla: [c008      ]

 	            País: [c009       ] [c016                     ]
                  Ciudad: [c010       ] [c017                     ]

     Representante Legal: [c013                          ]
        Paga Impuestos ?: [a]

}
SCREEN TITLE '    Compañía     '
{
                                                              [x003] de [y003]
                  Cliente: [c124      ][c125                                 ]
                 Contacto: [c103                          ]
               Referencia: [c104                                    ]
     Crédito Automático ?: [b]   Crédito Días: [c105  ]

 Cupo Crédito Moneda base: [c106            ]        Descuento en Facturación
 Cupo Crédito Moneda Alt.: [c107            ]        Dscto. rptos.: [c108  ]% 
                                                    Recargo rptos.: [c109  ]%
                                                  Contado Servicio: [c122  ]%
                                                  Crédito Servicio: [c123  ]%
                 Cheques ?: [d]
             Zona de Venta: [c110  ] [c116                          ]
             Zona de Cobro: [c111  ] [c117                          ]
  Aux. Cliente Moneda Base: [c112        ] [c118                             ]
  Aux. Cli. Moneda Alterna: [c113        ] [c119                             ]
 Aux. Anticipo Moneda Base: [c114        ] [c120                             ]
 Aux. Antic. Moneda Alter.: [c115        ] [c121                             ]
}
TABLES
cxct001, cxct002
ATTRIBUTES
c000 = cxct001.z01_codcli, AUTONEXT, REVERSE,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2',
	COMMENTS = 'El sistema asigna código automáticamente';
c001 = cxct001.z01_nomcli, AUTONEXT, REVERSE, UPSHIFT, WORDWRAP COMPRESS,
	COMMENTS = 'Indicar el nombre y los apellidos del cliente';
c002 = cxct001.z01_direccion1, AUTONEXT, REVERSE, UPSHIFT;
c003 = cxct001.z01_direccion2, AUTONEXT, UPSHIFT;
c004 = cxct001.z01_telefono1, AUTONEXT, REVERSE, UPSHIFT;
c005 = cxct001.z01_telefono2, AUTONEXT, UPSHIFT;
c006 = cxct001.z01_fax1, AUTONEXT, UPSHIFT;
c007 = cxct001.z01_fax2, AUTONEXT, UPSHIFT;
c008 = cxct001.z01_casilla, AUTONEXT, UPSHIFT;
c009 = cxct001.z01_pais, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c010 = cxct001.z01_ciudad, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c011 = cxct001.z01_tipo_clte, AUTONEXT, REVERSE,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c0b0 = cxct001.z01_personeria, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'RADIO', CONFIG = 'N Natural J Jurídica', DEFAULT = 'N';
c0c0 = cxct001.z01_tipo_doc_id, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'RADIO', CONFIG = 'C Cédula P Pasaporte R Ruc', DEFAULT = 'C';
c012 = cxct001.z01_num_doc_id, REVERSE, FORMAT = '###############';
c013 = cxct001.z01_rep_legal, AUTONEXT, UPSHIFT;
a    = cxct001.z01_paga_impto, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'CHECK', CONFIG = 'S N acknowledge', DEFAULT = 'S';
c014 = cxct001.z01_usuario, NOENTRY, UPSHIFT, REVERSE;
c015 = cxct001.z01_fecing, NOENTRY, UPSHIFT, REVERSE;
c    = FORMONLY.tit_est, NOENTRY, REVERSE, UPSHIFT;
c0a0 = FORMONLY.tit_estado_cli, NOENTRY, UPSHIFT;
c016 = FORMONLY.tit_pais, NOENTRY, REVERSE, UPSHIFT;
c017 = FORMONLY.tit_ciudad, NOENTRY, REVERSE, UPSHIFT;
c018 = FORMONLY.tit_tipo_cli, NOENTRY, REVERSE, UPSHIFT;

c124 = FORMONLY.tit_codigo_cli, NOENTRY, REVERSE;
c125 = FORMONLY.tit_nombre_cli, NOENTRY, REVERSE, UPSHIFT;
c103 = cxct002.z02_contacto, AUTONEXT, UPSHIFT;
c104 = cxct002.z02_referencia, AUTONEXT, UPSHIFT;
b    = cxct002.z02_credit_auto, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'CHECK', CONFIG = 'S N acknowledge', DEFAULT = 'S';
c105 = cxct002.z02_credit_dias, AUTONEXT, REVERSE, INCLUDE = (0 TO 65536);
c106 = cxct002.z02_cupocred_mb, AUTONEXT, INCLUDE = (0 TO 9999999999.99),
	REVERSE, FORMAT = '#,###,###,##&.##';
c107 = cxct002.z02_cupocred_ma, NOENTRY, REVERSE, FORMAT = '#,###,###,##&.##';
c108 = cxct002.z02_dcto_item_c, AUTONEXT, REVERSE, 
	INCLUDE = (0 TO 99.99), FORMAT = '--&.##';
c109 = cxct002.z02_dcto_item_r, AUTONEXT, REVERSE,
	INCLUDE = (0 TO 99.99), FORMAT = '--&.##';
c122 = cxct002.z02_dcto_mano_c, AUTONEXT, REVERSE,
	INCLUDE = (-99.99 TO 99.99), FORMAT = '--&.##';
c123 = cxct002.z02_dcto_mano_r, AUTONEXT, REVERSE, 
	INCLUDE = (-99.99 TO 99.99), FORMAT = '--&.##';
d    = cxct002.z02_cheques, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'CHECK', CONFIG = 'S N acknowledge', DEFAULT = 'S',
	COMMENTS = 'Indique si se le aceptan cheques al momento de pagar en caja';
c110 = cxct002.z02_zona_venta, AUTONEXT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c111 = cxct002.z02_zona_cobro, AUTONEXT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c112 = cxct002.z02_aux_clte_mb, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c113 = cxct002.z02_aux_clte_ma, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c114 = cxct002.z02_aux_ant_mb, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c115 = cxct002.z02_aux_ant_ma, AUTONEXT, REVERSE, UPSHIFT,
	WIDGET = 'FIELD_BMP', CONFIG = 'lista.bmp F2';
c116 = FORMONLY.tit_zona_vta, NOENTRY, UPSHIFT;
c117 = FORMONLY.tit_zona_cob, NOENTRY, UPSHIFT;
c118 = FORMONLY.tit_cli_mb, NOENTRY, REVERSE, UPSHIFT;
c119 = FORMONLY.tit_cli_ma, NOENTRY, REVERSE, UPSHIFT;
c120 = FORMONLY.tit_ant_mb, NOENTRY, REVERSE, UPSHIFT;
c121 = FORMONLY.tit_ant_ma, NOENTRY, REVERSE, UPSHIFT;

x001 = FORMONLY.vm_row_current1, NOENTRY;
y001 = FORMONLY.vm_num_rows1, NOENTRY;
x002 = FORMONLY.vm_row_current2, NOENTRY;
y002 = FORMONLY.vm_num_rows2, NOENTRY;
x003 = FORMONLY.vm_row_current3, NOENTRY;
y003 = FORMONLY.vm_num_rows3, NOENTRY;
END
