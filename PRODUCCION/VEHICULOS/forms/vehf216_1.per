database diteca
screen title '   Pre-venta   '
{                                                         
                                                             [x001] de [y001]
    Pre-venta No: [c002       ]                     Estado: [a|c050      ]
         Cliente: [c003       ] [c051                                    ]
        Vendedor: [c004  ] [c052                                         ]
          Moneda: [a0] [c053            ]  Paridad: [c006                ]
          Bodega: [a2] [c056            ]     Pago: [b             ]       
     Paga I.V.A.: [c][c059 ]%  

    [lbl1        ][lbl2             |lbl3  |lbl4          |lbl5           ]
   [c029       ] [c030             |c031  |c032          |c054            ]
   [c029       ] [c030             |c031  |c032          |c054            ]
   [c029       ] [c030             |c031  |c032          |c054            ]
   [c029       ] [c030             |c031  |c032          |c054            ]
   [c029       ] [c030             |c031  |c032          |c054            ]
   [c029       ] [c030             |c031  |c032          |c054            ]
                 [c012             ]      [c013          |c014            ]
                                                 I.V.A.: [c057            ]   
    Serie: [c035                    ]        Total Neto: [c058            ]
   Modelo: [c033            ]      
    Color: [c034       ]          
}

screen title ' Forma de pago '
{                                                          
                                                             [x002] de [y002] 

      Total Neto: [c038             ]   Cuotas   % Interés  Documentos a Favor
(-)Cuota inicial: [c016             ]  [c018  ]  [c017  ]   [c019             ] 
(-)  otros PA/NC: [c015             ]
                   -----------------
     Saldo Deuda: [c039             ]

      Código Plan: [c020  |c055                                  ]      
     Nro. vcmtos.: [c021  ]           Interés: [c022  ]%

      Transacción: [a1]          Número Transacción: [c023       ]

             Reserva de dominio:
             [c005                                         ]
             [c005                                         ]
             [c005                                         ]
             [c005                                         ]

   Ingresado Por: [c024      ]     Fecha de Ingreso: [c025               ]
}
end
tables
veht026
veht027
attributes
c002 = veht026.v26_numprev, NOENTRY, REVERSE,
	WIDGET='FIELD_BMP', CONFIG='lista.bmp F2';
a    = veht026.v26_estado, NOENTRY, UPSHIFT, REVERSE;
c003 = veht026.v26_codcli, REVERSE,
	WIDGET='FIELD_BMP', CONFIG='lista.bmp F2';
c004 = veht026.v26_vendedor, REVERSE,
	WIDGET='FIELD_BMP', CONFIG='lista.bmp F2';
c006 = veht026.v26_paridad, NOENTRY, FORMAT='#,###,##&.#########';
b    = veht026.v26_cont_cred, REVERSE, UPSHIFT, DEFAULT='R',
	WIDGET='RADIO', CONFIG='R Crédito C Contado';
a0   = veht026.v26_moneda, REVERSE, UPSHIFT, AUTONEXT,
	WIDGET='FIELD_BMP', CONFIG='lista.bmp F2';
a2   = veht026.v26_bodega, REVERSE, UPSHIFT, AUTONEXT,
	WIDGET='FIELD_BMP', CONFIG='lista.bmp F2';
c012 = veht026.v26_tot_bruto, REVERSE, NOENTRY, FORMAT='#,###,###,##&.##';
c013 = veht026.v26_tot_dscto, REVERSE, NOENTRY, FORMAT='###,###,##&.##';
c058 = veht026.v26_tot_neto, REVERSE, NOENTRY, FORMAT='#,###,###,##&.##';

c005 = veht026.v26_reserva, WORDWRAP COMPRESS, UPSHIFT;
c015 = formonly.resto_pa_nc, REVERSE, NOENTRY, FORMAT='#,###,###,##&.##';
c016 = veht026.v26_cuotai_fin, REVERSE, NOENTRY, FORMAT='###,###,##&.##';
c017 = veht026.v26_int_cuotaif, REVERSE, NOENTRY, FORMAT='#&.##';
c018 = veht026.v26_num_cuotaif, NOENTRY, REVERSE;
c019 = formonly.anticipos, NOENTRY, REVERSE, FORMAT='#,###,###,##&.##';
c020 = veht026.v26_codigo_plan, NOENTRY, REVERSE;
c021 = veht026.v26_num_vctos, NOENTRY, REVERSE;
c022 = veht026.v26_int_saldo, REVERSE, NOENTRY, FORMAT='#&.##';
a1 = veht026.v26_cod_tran, NOENTRY, UPSHIFT;
c023 = veht026.v26_num_tran, NOENTRY;

c024 = veht026.v26_usuario, REVERSE, UPSHIFT, NOENTRY;
c025 = veht026.v26_fecing, REVERSE, NOENTRY;

c029 = veht027.v27_codigo_veh, OPTIONS='-nolist',
	WIDGET='FIELD_BMP', CONFIG='lista.bmp F2';
c030 = veht027.v27_precio, NOENTRY, OPTIONS='-nolist', 
	FORMAT='#,###,###,##&.##';
c031 = veht027.v27_descuento, OPTIONS='-nolist', FORMAT='#&.##';
c032 = veht027.v27_val_descto, OPTIONS='-nolist',
	FORMAT='###,###,##&.##';
c054 = formonly.total, NOENTRY, OPTIONS='-nolist',
	FORMAT='#,###,###,##&.##';
c057 = formonly.valor_iva, NOENTRY, OPTIONS='-nolist',
	FORMAT='#,###,###,##&.##';
c014 = formonly.subtotal, NOENTRY, OPTIONS='-nolist',
	FORMAT='#,###,###,##&.##';

c033 = formonly.modelo, NOENTRY;
c034 = formonly.color, NOENTRY;
c035 = formonly.serie, NOENTRY;

c038 = formonly.total_neto, NOENTRY, REVERSE, FORMAT='###,###,##&.##';
c039 = formonly.sdo_credito, NOENTRY, REVERSE, FORMAT='###,###,##&.##';

c050 = formonly.n_estado,   NOENTRY;
c051 = formonly.n_cliente,  NOENTRY;
c052 = formonly.n_vendedor, NOENTRY;
c053 = formonly.n_moneda,   NOENTRY;
c055 = formonly.n_plan,     NOENTRY;
c056 = formonly.n_bodega,   NOENTRY;
c    = formonly.iva, NOENTRY, DEFAULT='N', UPSHIFT,
	WIDGET='CHECK', CONFIG='S N acknowledge';
c059 = formonly.porc_iva, NOENTRY, FORMAT='#&.##';

x001 = formonly.vm_row_current1 TYPE SMALLINT, NOENTRY;
y001 = formonly.vm_num_rows1    TYPE SMALLINT, NOENTRY;
x002 = formonly.vm_row_current2 TYPE SMALLINT, NOENTRY;
y002 = formonly.vm_num_rows2    TYPE SMALLINT, NOENTRY;

-- Botones
lbl1 = formonly.bt_codigo_veh, WIDGET='BUTTON', CONFIG='F15';
lbl2 = formonly.bt_precio_venta, WIDGET='BUTTON', CONFIG='F15';
lbl3 = formonly.bt_dscto, WIDGET='BUTTON', CONFIG='F15';
lbl4 = formonly.bt_val_dscto, WIDGET='BUTTON', CONFIG='F15';
lbl5 = formonly.bt_total, WIDGET='BUTTON', CONFIG='F15';

end

instructions
screen record ra_v27[6] (v27_codigo_veh, v27_precio, v27_descuento, v27_val_descto, total)

keys
F8 = 'Ver Vehículo'
F7 = 'Doc. a Favor'
F6 = 'Cuota Inicial'
F5 = 'Forma de Pago'
